ROM		EQU	$C000
RAM		EQU	$0000
RESET		EQU	$FFFE
IRQ		EQU	$FFF2
NMI		EQU	$FFF4
MASKNMI		EQU	%10111111
MASKIRQ		EQU	%11101111
MASKEDGE	EQU	%00100000
CONFIG		EQU	$1039


		ORG 	RAM
HORA		RMB	1
MINUTO		RMB	1
SEGUNDO		RMB	1
FLAGIRQ		RMB	1
FLAGNMI		RMB	1
		
		ORG	ROM
		TPA
		ANDA	#(MASKNMI&MASKIRQ)
		TAP
		;; IRQ por flanco
		LDAA	CONFIG
		ORAA	#MASKEDGE
		STAA	CONFIG
		CLR	FLAGIRQ
		CLR	FLAGNMI
		LDAA	#$58
		STAA	SEGUNDO
		STAA	MINUTO
		LDAA	#$23
		STAA	HORA

Main
		LDAA	FLAGNMI
		DB	$41
		BNE	RESETTIME
		LDAA	FLAGIRQ
		BEQ	Main
		CLR	FLAGIRQ
		JSR	INCSEC
		BRA	Main
RESETTIME
		LDAA	#$12
		STAA	HORA
		CLR	MINUTO
		CLR	SEGUNDO
		CLR	FLAGNMI
		BRA	Main

INCSEC
		LDAA 	SEGUNDO   ; $09
		ADDA 	#1        ; $0A
		DAA               ; $10
		STAA 	SEGUNDO
		CMPA	#$60
		BEQ	RESETSEG
		RTS
RESETSEG	CLR	SEGUNDO
		LDAA	MINUTO
		ADDA	#1
		DAA
		STAA	MINUTO
		CMPA	#$60
		BEQ	RESETMIN
		RTS
RESETMIN	CLR	MINUTO
		LDAA	HORA
		ADDA	#1
		DAA
		STAA	HORA
		CMPA	#$24
		BEQ	RESETHOR
		RTS
RESETHOR	CLR	HORA
		RTS


IRQ_ISR
		LDAA	#1
		STAA	FLAGIRQ
		RTI

NMI_ISR
		
		LDAA	#1
		STAA	FLAGNMI
		RTI

		ORG	RESET
		DW	ROM
		ORG	IRQ
		DW	IRQ_ISR
		ORG	NMI
		DW	NMI_ISR
		ORG	$FFF8
		DW	ROM